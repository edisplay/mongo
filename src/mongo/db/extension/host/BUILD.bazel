load("//bazel:mongo_src_rules.bzl", "mongo_cc_library", "mongo_cc_unit_test")
load("//bazel/mongot_extension_signing_key:mongot_extension_signing_key.bzl", "gpg_export_armored_key")
load("//bazel/mongot_extension_signing_key:mongot_extension_signing_key.bzl", "generate_embedded_public_key_header")

package(default_visibility = ["//visibility:public"])

exports_files([
    "document_source_extension_optimizable_test.cpp",
    "document_source_extension_for_query_shape_test.cpp",
])

exports_files(
    glob([
        "*.h",
    ]),
)

mongo_cc_library(
    name = "extension_host_utils",
    srcs = [
        "extension_host_utils.cpp",
    ],
    hdrs = [
        "extension_host_utils.h",
    ],
    deps = [
        "//src/mongo:base",
        "//src/mongo/db/commands/server_status:server_status_metric",
        "//src/mongo/db/extension/shared",
        "//src/mongo/db/extension/shared/handle",
    ],
)

# Note that this is a standalone library such that it doesn't need to have any server dependencies like the rest of the extension_host target does.
mongo_cc_library(
    name = "extension_operation_metrics_registry",
    srcs = [
        "operation_metrics_registry.cpp",
    ],
    hdrs = [
        "operation_metrics_registry.h",
    ],
    deps = [
        ":extension_host_utils",
        "//src/mongo:base",
        "//src/mongo/db/extension/shared/handle/aggregation_stage:aggregation_stage_handles",
    ],
)

# Standalone library for vector search metrics to avoid circular dependencies.
# Both pipeline and extension_host can depend on this.
mongo_cc_library(
    name = "extension_vector_search_server_status",
    srcs = [
        "extension_vector_search_server_status.cpp",
    ],
    hdrs = [
        "extension_vector_search_server_status.h",
    ],
    deps = [
        "//src/mongo:base",
        "//src/mongo/db/commands/server_status:server_status_core",
    ],
)

mongo_cc_library(
    name = "extension_host",
    srcs = [
        "document_source_extension_for_query_shape.cpp",
        "document_source_extension_optimizable.cpp",
        "document_source_visitor_docs_needed_bounds.cpp",
        "extension_stage.cpp",
    ],
    deps = [
        ":extension_host_utils",
        ":extension_operation_metrics_registry",
        ":extension_vector_search_server_status",
        "//src/mongo:base",
        "//src/mongo/db:commands",
        "//src/mongo/db/commands/server_status:server_status_core",
        "//src/mongo/db/extension/host/aggregation_stage",
        "//src/mongo/db/extension/host_connector/adapter:host_adapters",
        "//src/mongo/db/extension/host_connector/adapter:host_services_adapter",
        "//src/mongo/db/extension/host_connector/handle:host_handles",
        "//src/mongo/db/extension/shared/handle/aggregation_stage:aggregation_stage_handles",
        "//src/mongo/db/pipeline",
        "//src/mongo/db/pipeline:docs_needed_bounds_visitor",
        "//src/mongo/db/pipeline:lite_parsed_desugarer",
    ],
)

gpg_export_armored_key(
    name = "mongot_extension_signing_key_asc",
    armored_key_output_file = "mongot-extension.asc",
    key = "@mongot_extension_signing_key//:mongot-extension.pub",
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

generate_embedded_public_key_header(
    name = "embed_mongot_key",
    embedded_key_header_path = "mongot_extension_signing_key.h",
    public_key_path = "mongot-extension.asc",
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

mongo_cc_library(
    name = "signature_validator",
    srcs = [
        "signature_validator.cpp",
    ],
    hdrs = [":embed_mongot_key"],
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        "//src/mongo:base",
    ],
)

mongo_cc_library(
    name = "extension_loader",
    srcs = [
        "load_extension.cpp",
        "load_stub_parsers.cpp",
        "//src/mongo/db/pipeline:document_source_list_extensions.cpp",
    ],
    deps = [
        ":extension_host",
        "//src/mongo:base",
        "//src/mongo/db/query:query_knobs",
        "//src/third_party/yaml-cpp:yaml",
    ] + select({
        "//bazel/config:not_linux": [],
        "//conditions:default": [":signature_validator"],
    }),
)

mongo_cc_unit_test(
    name = "extensions_api_test",
    srcs = [
        "load_extension_test.cpp",
        "load_native_vector_search_test.cpp",
    ],
    # Data lists the targets that must be built to generate extension shared libraries, which are
    # loaded in the unit tests.
    data = [
        "//src/mongo/db/extension/test_examples:load_extension_test_extensions",
        "//src/mongo/db/extension/test_examples/test_extensions_signing_keys:test_extensions_signing_public_key.asc",
    ],
    tags = ["mongo_unittest_seventh_group"],
    target_compatible_with = select({
        "//bazel/config:shared_archive_or_link_dynamic": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }) + select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        ":extension_loader",
        "//src/mongo/db/pipeline:aggregation_context_fixture",
        "//src/mongo/unittest",
    ],
)
